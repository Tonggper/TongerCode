!EX10.34 螺栓连接柱的屈曲分析

!N个螺栓连接，且有常规接触

finish
/clear,all
/prep7
t1=10
t2=20
h=800
b=200
et,1,solid45
et,2,conta173
et,3,targe170
mp,ex,1,2.1e5
mp,prxy,1,0.0
mp,mu,2,0.2
!几何模型与网格划分
blc4,,,b,h,t2
wpoff,,,t2
blc4,,,b,h,t1
vgen,2,2,,,,,-(t1+t2)
vatt,1,,1
esize,t1/2
lsel,s,length,,b
lesize,all,2*t1
lsel,s,length,,h
lesize,all,2*t1
lsel,all
vmesh,all
!创建接触对
!板2、3之间
real,2
mat,2
type,2
vsel,s,,,1
aslv,s
asel,r,loc,z,0
nsla,s,1
esurf
vsel,s,,,3
aslv,s
asel,r,loc,z,0
nsla,s,1
type,3
esurf
!板1、2之间
real,3
vsel,s,,,1
aslv,s
asel,r,loc,z,t2
nsla,s,1
type,2
esurf
vsel,s,,,2
aslv,s
asel,r,loc,z,t2
nsla,s,1
type,3
esurf

!创建N个螺栓连接
nbo=3   !nbo个螺栓
et,4,beam188 !柔性梁连接
sectype,1,beam,csolid !csolid实心圆截面
secdata,10 !Φ20螺栓
type,4
mat,1
secnum,1

!点焊节点编号(nbo个)
swn1=ndinqr(0,14)+1  !ndinqr(0,14)为获取最大节点号
*do,i,2,nbo*2
swn%i%=swn%i-1%+1
*enddo
!创建点焊节点
*do,i,1,nbo
yi=h/(nbo+1)*i
n,swn%2*i-1%,b/2,yi,t1+t2
n,swn%2*i%,b/2,yi,-t1
*enddo
allsel,all
!创建点焊集
*do,i,1,nbo
swgen,sw%i%,10,8,13,swn%2*i-1%,swn%2*i%,15
*enddo

esel,s,type,,4
edele,all


*do,i,1,nbo
fill,swn%2*i-1%,swn%2*i%,1,,100
*enddo
type,4
mat,1
secnum,1

wpcsy,-1
allsel,all
/view,1,1,1,1
*do,i,1,nbo
esel,none
e,swn%2*i-1%,swn%2*i-1%+100
e,swn%2*i-1%+100,swn%2*i%
psmesh,i+100,,,all,,0,z,10
*enddo

/solu
nsubst,200
AUTOTS,ON
LNSRCH,ON
PRED,ON
nlgeom,on
cnvtol,u,-1
cnvtol,M,-1
cnvtol,f,,0.05
outres,all,all

time,1
*do,i,1,nbo
sload,i+100,9,tiny,forc,180000,2,3
*enddo

asel,s,,,4
da,all,ux,0
da,all,uz,0
da,all,uy,0
allsel,all
solve

time,2
solve

time,3
allsel,all
asel,s,,,4
da,all,uy,10
asel,s,,,9,15,6
da,all,uy,0
allsel,all
solve


!后处理
/post1
suball=0
*do,i,1,3
  SET,i,LAST
  *get,sub_%i%_last,active,0,solu,ncmss 
  suball=suball+sub_%i%_last
*enddo

*dim,Displace,,suball
!!建立最大荷载子步数行、1列的数组Displace_Z，用于存储位移最大值节点的位移
*dim,Force,,suball 
!!建立最大荷载子步数行、1列的数组Force，用于存储荷载

!令sub_last=最大荷载子步数
suball=0

*do,i,1,3
  *do,j,1,sub_%i%_last
  set,i,j
  asel,s,,,9,15,6
  nsla,s,1
  Fsum
  *get,force_y,Fsum,Fy
  Force(%suball+j%)=force_y
  allsel,all
  *get,displace_y,node,940,u,y
  Displace(%suball+j%)=displace_y
  *enddo
  suball=suball+sub_%i%_last
*enddo


*use,datawrite_beam.mac
