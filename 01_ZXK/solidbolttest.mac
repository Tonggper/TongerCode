finish
/clear,all
/prep7
t1=10
t2=20
h=800
b=200
et,1,solid45
et,2,conta173
et,3,targe170
mp,ex,1,2.1e5
mp,prxy,1,0.0
mp,mu,2,0.2
!几何模型与网格划分
blc4,,,b,h,t2
wpoff,,,t2
blc4,,,b,h,t1
vgen,2,2,,,,,-(t1+t2)
cm,plates,volume
!
vsel,none
wpcsy,-1
wpoffs,b/2,h/4,-t1
cyl4,,,22/2,,,,100
wpoffs,,h/4,
cyl4,,,22/2,,,,100
wpoffs,,h/4,
cyl4,,,22/2,,,,100
cm,cyl,volume
allsel,all
vsbv,plates,cyl,
!
vsel,none
wpoffs,,,-15
cyl4,,,20/2,,,,70
cyl4,,,30/2,,,,15
wpoffs,,,55
cyl4,,,30/2,,,,15
vgen,3,all,,,,-h/4,,,,
vptn,all
wpoffs,,,-20
vsbw,all
cm,bolt,volume
!划分网格
allsel,all
vatt,1,,1
esize,t1/2
lsel,s,length,,b
lesize,all,2*t1
lsel,s,length,,h
lesize,all,2*t1
allsel,all
vsweep,all

!创建接触对
!板2、3之间
allsel,all
real,2
mat,2
type,2
vsel,s,,,9
aslv,s
asel,r,loc,z,0
nsla,s,1
esurf
vsel,s,,,7
aslv,s
asel,r,loc,z,0
nsla,s,1
type,3
esurf
!板1、2之间
real,3
vsel,s,,,7
aslv,s
asel,r,loc,z,t2
nsla,s,1
type,2
esurf
vsel,s,,,8
aslv,s
asel,r,loc,z,t2
nsla,s,1
type,3
esurf
!螺母、板1之间
real,4
cmsel,s,bolt
aslv,s
asel,r,loc,z,t1+t2
nsla,s,1
type,2
esurf
vsel,s,,,8
aslv,s
asel,r,loc,z,t1+t2
nsla,s,1
type,3
esurf
!螺母、板3之间
real,5
cmsel,s,bolt
aslv,s
asel,r,loc,z,-t1
nsla,s,1
type,2
esurf
vsel,s,,,9
aslv,s
asel,r,loc,z,-t1
nsla,s,1
type,3
esurf
!螺栓孔壁、螺杆之间
real,6
cmsel,s,bolt
aslv,s
asel,r,loc,z,-t1+1,t1+t2-1

*do,i,1,3
local,10+i,1,b/2,h/4*i,
csys,10+i
allsel,all
cmsel,u,bolt
aslv,s
asel,r,loc,x,22/2
nsla,s,1
type,3
esurf
*enddo
allsel,all

!创建预紧截面
csys,0
wpcsy,-1
*do,i,1,3
allsel,all
cmsel,s,bolt
vsel,r,loc,y,h/4*i-50,h/4*i+50
eslv,s
psmesh,i,,,all,,0,z,10
*enddo
allsel,all
save,ss1,sb





/solu
nsubst,200
AUTOTS,ON
LNSRCH,ON
PRED,ON
nlgeom,on
cnvtol,u,-1
cnvtol,M,-1
cnvtol,f,,0.05
outres,all,all

time,1
*do,i,1,3
sload,i,9,tiny,forc,180000,2,3
*enddo
asel,s,,,4
da,all,ux,0
da,all,uz,0
da,all,uy,0
allsel,all
solve

time,2
solve

time,3
allsel,all
asel,s,,,4
da,all,uy,10
asel,s,,,9,15,6
da,all,uy,0
allsel,all
solve

!后处理
/post1
suball=0
*do,i,1,3
  SET,i,LAST
  *get,sub_%i%_last,active,0,solu,ncmss 
  suball=suball+sub_%i%_last
*enddo

*dim,Displace,,suball
!!建立最大荷载子步数行、1列的数组Displace_Z，用于存储位移最大值节点的位移
*dim,Force,,suball 
!!建立最大荷载子步数行、1列的数组Force，用于存储荷载

!令sub_last=最大荷载子步数
suball=0

*do,i,1,3
  *do,j,1,sub_%i%_last
  set,i,j
  asel,s,,,9,15,6
  nsla,s,1
  Fsum
  *get,force_y,Fsum,Fy
  Force(%suball+j%)=force_y
  allsel,all
  *get,displace_y,node,4907,u,y
  Displace(%suball+j%)=displace_y
  *enddo
  suball=suball+sub_%i%_last
*enddo

*use,datawrite_solid.mac










